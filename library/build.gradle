plugins {
    id 'maven-publish'
    id 'com.android.library'
    id 'kotlin-android'
    id 'signing'
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 33
        versionCode 1
        versionName rootProject.publishVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.archivesBaseName = "$rootProject.artifactPrefix-library-$versionName"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0',
            'androidx.recyclerview:recyclerview:1.0.0'

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

repositories {
    mavenCentral()
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.implementation
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

//Creating sources with comments
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
//    archives javadocJar
    archives androidSourcesJar
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = rootProject.groupId
            artifactId = "$rootProject.artifactPrefix-library"
            version = rootProject.publishVersion

            pom {
                name = "$rootProject.projectName Library"
                description = 'Blueprint is a compact framework for constructing mvp architectures in Android within a scrollable, multi-view-type list UI'
                url = 'https://github.com/Comcast/blueprint'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dapp'
                        name = 'Mark Dappollone'
                    }
                }
                scm {
                    url = 'scm:git@github.com:Comcast/blueprint.git'
                    connection = 'scm:git@github.com:Comcast/blueprint.git'
                    developerConnection = 'scm:git@github.com:Comcast/blueprint.git'
                }
            }
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}