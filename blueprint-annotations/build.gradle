plugins {
    id 'org.jetbrains.kotlin.jvm'
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            repository(url: "file:///Users/mdappo200/.m2/repository/")

            pom.project {
                groupId rootProject.groupId
                name "$rootProject.projectName Annotations"
                version rootProject.publishVersion
                packaging 'jar'
                description 'Annotations Library for Blueprint, a compact framework for constructing mvp architectures in Android'
                artifactId "$rootProject.artifactPrefix-annotations"
                url 'https://github.com/Comcast/blueprint'

                scm {
                    url 'scm:git@github.com:Comcast/blueprint.git'
                    connection 'scm:git@github.com:Comcast/blueprint.git'
                    developerConnection 'scm:git@github.com:Comcast/blueprint.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dapp'
                        name 'Mark Dappollone'
                    }
                }
            }
        }
    }
}
repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
