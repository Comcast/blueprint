plugins {
    id 'maven-publish'
    id 'com.android.library'
    id 'kotlin-android'
    id 'signing'
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 33
        versionCode 1
        versionName rootProject.publishVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.archivesBaseName = "$rootProject.artifactPrefix-architecture-$versionName"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar']),
                   project(':library')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'in.srain.cube:ultra-ptr:1.0.11'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

repositories {
    mavenCentral()
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

//Creating sources with comments
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
//    archives javadocJar
    archives androidSourcesJar
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = rootProject.groupId
            artifactId = "$rootProject.artifactPrefix-architecture"
            version = rootProject.publishVersion

            pom {
                name = "$rootProject.projectName Architecture"
                description = 'A collection of architectural components for accelerating Android development with the Blueprint Framework'
                url = 'https://github.com/Comcast/blueprint'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dapp'
                        name = 'Mark Dappollone'
                    }
                }
                scm {
                    url = 'scm:git@github.com:Comcast/blueprint.git'
                    connection = 'scm:git@github.com:Comcast/blueprint.git'
                    developerConnection = 'scm:git@github.com:Comcast/blueprint.git'
                }
            }
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}
